project.description = 'Library for sending notifications about autotest results (to generate executable Fat JAR)'

dependencies {
    annotationProcessor(libs.lombok)
    compileOnly(libs.lombok)

    implementation project(':allure-notifications-api')

    implementation platform(libs.slf4j.bom)
    implementation(libs.slf4j.jul) // Java Util Logging -> SLF4J
    implementation(libs.slf4j.jcl) // commons-logging -> SLF4J

    implementation platform(libs.log4j.bom)
    implementation(libs.log4j.api)
    implementation(libs.log4j.core)
    implementation(libs.log4j.slf4j2.impl)
}

configurations.configureEach {
    exclude module: 'commons-logging' // Use jcl-over-slf4j bridge instead
}

jar {
    dependsOn(configurations.runtimeClasspath)
    manifest {
        attributes(
                'Main-Class': 'guru.qa.allure.notifications.Application'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.WARN
}
